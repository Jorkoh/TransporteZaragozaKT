buildscript {
    ext.buildConfig = [
            compileSdk  : 28,
            targetSdk   : 28,
            minSdk      : 21,
            gradlePlugin: '3.3.2'
    ]

    ext.versions = [
            kotlin          : '1.3.21',
            androidx        : '1.1.0-alpha02',
            maps            : '16.1.0',
            location        : '16.0.0',
            constraintlayout: '1.1.3',
            colorpicker     : '1.0.5',
            junit           : '4.12',
            mockito_kotlin  : '2.0.0',
            mockito_core    : '2.23.4',
            room            : '2.1.0-alpha04',
            navigation      : '2.1.0-alpha01',
            workmanager     : '1.0.0',
            parse           : '1.19.0',
            lottie          : '3.0.0'
    ]

    ext.deps = [
            'androidx'    : [
                    'appCompat'       : "androidx.appcompat:appcompat:${versions.androidx}",
                    'constraintLayout': "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
                    'preferences'     : "androidx.preference:preference:${versions.androidx}",
                    'recyclerView'    : "androidx.recyclerview:recyclerview:${versions.androidx}",
                    'room'            : [
                            'runtime'   : "androidx.room:room-runtime:${versions.room}",
                            'compiler'  : "androidx.room:room-compiler:${versions.room}",
                            'coroutines': "androidx.room:room-coroutines:${versions.room}"
                    ],
                    'navigation'      : [
                            'runtime' : "androidx.navigation:navigation-runtime-ktx:${versions.navigation}",
                            'fragment': "androidx.navigation:navigation-fragment-ktx:${versions.navigation}",
                            'ui'      : "androidx.navigation:navigation-ui-ktx:${versions.navigation}"
                    ]
            ],
            'kotlin'      : [
                    'stdlib': [
                            'jdk': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
                    ]
            ],
            'playServices': [
                    'maps'    : "com.google.android.gms:play-services-maps:${versions.maps}",
                    'location': "com.google.android.gms:play-services-location:${versions.location}"
            ],
            'material'    : "com.google.android.material:material:${versions.androidx}",
            'colorpicker' : "com.jaredrummler:colorpicker-compat:${versions.colorpicker}",
            'junit'       : "junit:junit:${versions.junit}",
            'mockito'     : [
                    'core'  : "org.mockito:mockito-core:${versions.mockito_core}",
                    'kotlin': "com.nhaarman.mockitokotlin2:mockito-kotlin:${versions.mockito_kotlin}"
            ],
            'workmanager' : "android.arch.work:work-runtime-ktx:${versions.workmanager}",
            'parse'       : "com.github.parse-community.Parse-SDK-Android:parse:${versions.parse}",
            'lottie'      : "com.airbnb.android:lottie:${versions.lottie}"
    ]

    repositories {
        google()
        jcenter()
        maven { url "http://jitpack.io/" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${buildConfig.gradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://jitpack.io/" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion versions.kotlin
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
