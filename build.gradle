buildscript {
    // ---- DEPENDENCIES ----
    // @formatter:off
    ext.buildConfig = [
            compileSdk              : 28,
            targetSdk               : 28,
            minSdk                  : 21,
            gradlePlugin            : '3.3.1'
    ]

    ext.versions = [
            kotlin                  : '1.3.21',
            androidx                : '1.1.0-alpha02',
            constraintlayout        : '1.1.3',
            colorpicker             : '1.0.5',
            junit                   : '4.12',
            mockito_kotlin          : '2.0.0',
            mockito_core            : '2.23.4'
    ]

    ext.deps = [
            'androidx': [
                    'appCompat'         : "androidx.appcompat:appcompat:${versions.androidx}",
                    'constraintLayout'  : "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
                    'preferences'       : "androidx.preference:preference:${versions.androidx}",
                    'recyclerView'      : "androidx.recyclerview:recyclerview:${versions.androidx}"
            ],
            'kotlin': [
                    'stdlib': [
                            'jdk'           : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
                    ]
            ],
            'material'              : "com.google.android.material:material:${versions.androidx}",
            'colorpicker'           : "com.jaredrummler:colorpicker-compat:${versions.colorpicker}",
            'junit'                 : "junit:junit:${versions.junit}",
            'mockito': [
                    'core'              : "org.mockito:mockito-core:${versions.mockito_core}",
                    'kotlin'            : "com.nhaarman.mockitokotlin2:mockito-kotlin:${versions.mockito_kotlin}"
            ]
    ]

    repositories {
        google()
        jcenter()
        maven { url "http://jitpack.io/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${buildConfig.gradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://jitpack.io/" }
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion versions.kotlin
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
